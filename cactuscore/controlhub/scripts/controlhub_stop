#!/bin/env escript
%% -*- erlang -*-
%%! -sname tempshellstop -setcookie ch_cookie -hidden

%###############################################################################
% Stops the Control Hub if it's running.
%
% This script has the following return codes:
%   * Return code is 0 if the Control Hub was stopped successfully.
%   * Return code is 1 if the Control Hub was already stopped.
%   * Return code is 2 if there was an error during the remote procedure call.
%   * Return code is 3 if there was a general error during stopping.
%
%
% Robert Frazier, June 2012
%###############################################################################

main(_Ignore) ->
    {ok, Hostname} = inet:gethostname(),
    Node = list_to_atom("controlhub@" ++ Hostname),
    ReturnCode = case net_adm:ping(Node) of
                     pang ->
                         io:format("Control Hub already stopped~n"),
                         1;
                     pong ->
                         io:format("Stopping Control Hub... "),
                         case rpc:call(Node, init, stop, [0]) of
                             ok ->
                                 wait_for_node_death(Node),
                                 io:format("ok~n"),
                                 0;
                             {badrpc, Reason} ->
                                 io:format("Failed!~n  ==> Error running remote procedure call: ~p~n", [Reason]),
                                 2;
                             Else ->
                                 io:format("Failed!~n  ==> Error: ~p~n", [Else]),
                                 3
                         end
                 end,
    halt(ReturnCode).

%% Loops around with 0.2 second delay only returning when controlhub node can't be contacted anymore
wait_for_node_death(Node) when is_atom(Node) ->
    timer:sleep(200),
    case net_adm:ping(Node) of
        pang ->
            ok;
        pong ->
            wait_for_node_death(Node)
    end.
    
